{
    parserClass="com.farragofiction.ideaplugin.wordlist.parser.WordListParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="WordList"
    psiImplClassSuffix="Impl"
    psiPackage="com.farragofiction.ideaplugin.wordlist.psi"
    psiImplPackage="com.farragofiction.ideaplugin.wordlist.psi.impl"

    elementTypeHolderClass="com.farragofiction.ideaplugin.wordlist.psi.WordListTypes"
    elementTypeClass="com.farragofiction.ideaplugin.wordlist.psi.WordListElementType"
    tokenTypeClass="com.farragofiction.ideaplugin.wordlist.psi.WordListTokenType"
}

wordListFile ::= HEADER item_*

private item_ ::= (fileInclude|fileDefault|list|COMMENT|CRLF)

indent ::= "    "

fileInclude ::= INCLUDE FILENAME

fileDefault ::= DEFAULT DEFAULT_SECTION

// list

variant ::= indent indent property

listEntryTitle ::= property

list ::= LISTNAME (listInclude|listDefault|listEntry)*(listEntry)+(listInclude|listDefault|listEntry)*

listEntry ::= indent listEntryTitle variant*

listInclude ::= indent INCLUDE listWeight

listDefault ::= indent DEFAULT property

property ::= (KEY SEPARATOR VALUE) | KEY

listWeight ::= (LISTNAME SEPARATOR VALUE) | LISTNAME